# Pimlico | Inigo Selwood 2022 (c)

__root__ := (element __newline__+)* element __newline__*

element := 
    - comment
    - macro
    - rule

comment := '#' __character__*

macro :=
    - '.include' '\"' . (!'\"' __character__)+ . '\"'
    - '.include' '<' . (!'>' __character__)+ . '>'

rule := __identifier__ ruleType? ':=' production
ruleType := '<' . (!'>' __character__)+ ruleType? . '>'

production := sequence productionExpression?
productionExpression := '{{' (!'}}' __character__ | __newline__)+ '}}'

term :=
    - choice
    - constant
    - intrinsic
    - range
    - reference
    - sequence
    - set

choice := term ('|' term)+

constant := '\'' . (!'\'' escapeCode | __character__)+ . '\''

intrinsic := 
    - '__character__'
    - '__identifier__'
    - '__integer__'
    - '__newline__'
    - '__number__'

range := '[' . rangeCharacter{2} . ']'
rangeCharacter := rangeEscapeCode | __character__
rangeEscapeCode := '\' \'' | '\\]' | escapeCode

reference := __identifier__

sequence := term{2:}

set := '`' . (!'`' setEscapeCode | __character__)+ . '`'
setEscapeCode := '\\`' | escapeCode

escapeCode := '\\\'' | '\\\"' | '\\\\' | '\\t' | '\\v' | '\\n' | '\\r'